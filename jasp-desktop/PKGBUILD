# Maintainer: sukanka <su975853527 AT gmail dot com>

pkgname=jasp-desktop
_pkgname=jasp
_cran="https://mirrors.bfsu.edu.cn/CRAN"
pkgver=0.16.2
pkgrel=1
pkgdesc="A complete statistical package for both Bayesian and Frequentist statistical methods"
arch=('any')
url="https://github.com/hkrn/nanoem"
license=('AGPL3')
makedepends=("cmake" 'boost' 'jsoncpp'
'openssl'
'autoconf'
'zlib'
'bison'
'flex'
'jags'
'gcc-fortran'
'qtcreator'

# 'r-jaspbase'

)
depends=('r'
'qt6-5compat'
'readstat'
'libarchive'
'r-rinside'
'qt6-base'
)
provides=($_pkgname)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jasp-stats/jasp-desktop/archive/refs/tags/v0.16.2.tar.gz"
"${_pkgname}".patch
"jaspColumnEncoder::git+https://github.com/jasp-stats/jaspColumnEncoder.git#commit=aaacc69"
'jaspResults::git+https://github.com/jasp-stats/jaspResults.git#commit=fdcd76'
'jaspBase::git+https://github.com/jasp-stats/jaspBase.git#commit=026404c'
'jaspGraphs::git+https://github.com/jasp-stats/jaspGraphs.git#commit=1e39c726'
'jaspTools::git+https://github.com/jasp-stats/jaspTools.git#commit=98953594'
)
sha256sums=('6f62db1b2b0741c894a7937f413799887e0d443f25f1b85d59e914847b14fff1'
            '33ced4ef825eaa656dc527ff10abefb2653a37debcc914ca2d97f8461fbae847'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare(){
    cd $srcdir/${pkgname}-${pkgver}
    patch --strip=1 < ../${_pkgname}.patch
    cp -rf $srcdir/jaspColumnEncoder/*  Common/jaspColumnEncoder
    cp -rf $srcdir/jaspResults/*        R-Interface/jaspResults
    cp -rf $srcdir/jaspBase/*           Engine/jaspBase
    cp -rf $srcdir/jaspGraphs/*         Engine/jaspGraphs
    cp -rf $srcdir/jaspTools/*          Tools/jaspTools
    
    find Tools/CMake -name *.cmake -print0 | xargs -0 sed -i "s|/usr/local|/usr|g"
}


build(){
    cd $srcdir/${pkgname}-${pkgver}
    mkdir -p ${srcdir}/usr/lib/R
    cmake -S . -B build -DCUSTOM_R_PATH=/usr/lib/R -DLINUX_LOCAL_BUILD=OFF -DINSTALL_R_MODULES=OFF \
        -DUSE_LOCAL_R_LIBS_PATH=",lib=\"${srcdir}/usr/lib/R\"" \
        -DR_REPOSITORY="${_cran}" \
        -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib 
        
    cmake --build build
}

package() {
     cd $srcdir/${pkgname}-${pkgver}/build
     make install DESTDIR=${pkgdir}
     sed -i "s|^Exec.*|Exec=JASP %f|g" \
        ${pkgdir}/usr/share/applications/org.jaspstats.JASP.desktop
        
    # fix RPATH
    patchelf --add-rpath /usr/lib/R/library/RInside/libs/ ${pkgdir}/usr/bin/jaspengine
    
    rm -rf ${pkgdir}/usr/lib/jasp-desktop/{renv-root,renv-cache}
}
